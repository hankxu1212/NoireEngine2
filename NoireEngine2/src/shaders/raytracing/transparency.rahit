#version 460

#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_ARB_gpu_shader_int64 : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require

#include "transparency.glsl"
#include "../host.glsl"

hitAttributeEXT vec2 attribs;

layout(location = 0) rayPayloadInEXT hitPayload prd;

layout(set = 5, binding = 0) uniform accelerationStructureEXT topLevelAS;

#include "../glsl/materials.glsl"

#define MAX_REFLECTION_LOD 6
const vec3 Fdielectric = vec3(0.04);
#include "../glsl/pbr.glsl"

layout(constant_id = 0) const int MATERIAL_TYPE = 0;

void main()
{
    // Object data
    ObjectDesc objResource = OBJECTS[gl_InstanceCustomIndexEXT];
    Indices indices = Indices(objResource.indexAddress);
    Vertices vertices = Vertices(objResource.vertexAddress);

    // Indices of the triangle
    ivec3 ind = indices.i[gl_PrimitiveID];

    // Vertex of the triangle
    Vertex v0 = vertices.v[ind.x];
    Vertex v1 = vertices.v[ind.y];
    Vertex v2 = vertices.v[ind.z];

    const vec3 barycentrics = vec3(1.0 - attribs.x - attribs.y, attribs.x, attribs.y);

    // Computing the coordinates of the hit position
    const vec3 pos      = v0.pos * barycentrics.x + v1.pos * barycentrics.y + v2.pos * barycentrics.z;
    const vec3 worldPos = vec3(gl_ObjectToWorldEXT * vec4(pos, 1.0));  // Transforming the position to world space

    // Computing the normal at hit position
    const vec3 nrm      = v0.nrm * barycentrics.x + v1.nrm * barycentrics.y + v2.nrm * barycentrics.z;
    const vec3 n = normalize(vec3(nrm * gl_WorldToObjectEXT));  // Transforming the normal to world space

    const vec2 UV = v0.texCoord * barycentrics.x + v1.texCoord * barycentrics.y + v2.texCoord * barycentrics.z;

    vec3 color;
    if (MATERIAL_TYPE == 0) // lambertian
    {
        LambertianMaterial material = LAMBERTIAN_MATERIAL_ReadFrombuffer(objResource.offset);
        prd.done = 1;
        prd.hitValue = material.albedo.rgb;
    }
    else if (MATERIAL_TYPE == 1) // pbr
    {
        PBRMaterial material = PBR_MATERIAL_ReadFrombuffer(objResource.offset);
        prd.done = 1;
        prd.hitValue = material.albedo.rgb;
    }
    else if (MATERIAL_TYPE == 2)
    {
        prd.done = 0;
        // Compute refraction direction
        float eta = (dot(-gl_WorldRayDirectionEXT, n) > 0.0) ? 1.0 / 1.5 : 1.5; // Adjust eta based on entry/exit
        vec3 refractedDir = refract(gl_WorldRayDirectionEXT, n, eta);
        prd.rayOrigin = worldPos;
        prd.rayDir = refractedDir;
    }
}
